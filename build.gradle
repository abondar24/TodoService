plugins {
    id "java"
    id "idea"
    id "org.springframework.boot" version "$springBootVersion"
    id "io.freefair.lombok" version "$lombokVersion"
    id 'org.graalvm.buildtools.native' version "$nativeVersion"
    id 'com.google.cloud.tools.jib' version "$jibVersion"
}

apply plugin: "java"
apply plugin: "io.spring.dependency-management"

group "org.abondar.industrial"
version '1.2-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-validation")

    implementation("org.flywaydb:flyway-core:$flywayVersion")
    implementation("net.logstash.logback:logstash-logback-encoder:$logstashVersion")
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocVersion")
    runtimeOnly("com.h2database:h2")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    imageName = "$imageName"
}

graalvmNative {
    binaries {
        main {
            imageName.set('todoservice')
            buildArgs.add('--verbose')
        }
    }
}


jib {
    from {
        image = "ghcr.io/graalvm/jdk:ol8-java17-22.3.1"
    }
    to {
        image = "$imageName"
        tag = project.version
        auth {
            username = "$dockerhubUser"
            password = "$dockerhubPass"
        }
    }

    container {
        mainClass = "org.abondar.industrial.todo.TodoApplication"
        ports = ["8080"]
    }

}

